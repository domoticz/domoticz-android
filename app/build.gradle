apply plugin: 'com.android.application'


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    def versionPropsFile = file('version.properties')

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0
        def runTasks = gradle.startParameter.taskNames

        println "****************************"
        println "runTasks: ${gradle.startParameter.taskNames}"
        if ('assembleRelease' in runTasks || 'aR' in runTasks || ':app:compilePaidReleaseSources' in runTasks || ':app:generatePaidReleaseSources' in runTasks || ':app:assemblePaidRelease' in runTasks || ':app:compileoottReleaseSources' in runTasks || ':app:generateoottReleaseSources' in runTasks || ':app:assembleoottRelease' in runTasks) {
            value = 1
            println "Build Version: +1"
        }
        println "****************************"

        def versionMajor = 0
        def versionMinor = 1
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        def version_Code = versionProps['VERSION_CODE'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_CODE'] = version_Code.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            versionCode version_Code
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
            minSdkVersion 16
            targetSdkVersion 28
            multiDexEnabled true
            wearAppUnbundled true
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    //noinspection GroovyAssignabilityCheck
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    flavorDimensions "tier"
    productFlavors {
        free {
            applicationId 'nl.hnogames.domoticz'
            resValue "string", "APK_VALIDATE_DEBUG", "$System.env.APK_VALIDATE_DEBUG"
            resValue "string", "APK_VALIDATE_PROD", "$System.env.APK_VALIDATE_PROD"
            resValue "string", "APK_LICENSE_PREMIUM", "$System.env.APK_LICENSE_PREMIUM"

            resValue "string", "ADMOB_COMP_KEY", "$System.env.ADMOB_COMP_KEY"
            resValue "string", "ADMOB_APP_KEY", "$System.env.ADMOBAPPKEY"
            resValue "string", "ADMOB_ADS_KEY", "$System.env.ADMOBADSKEY"

            resValue "string", "analiticsapikey", "$System.env.GOOGLE_ANALITICS_ID"
            resValue "string", "googleapikey", "$System.env.GOOGLEAPIKEY"
            resValue "string", "app_name_domoticz", 'Domoticz Lite'
            buildConfigField "boolean", "LITE_VERSION", "true"
            buildConfigField "boolean", "PAID_OOTT", "false"
            dimension "tier"
        }
        paid {
            applicationId 'nl.hnogames.domoticz.premium'

            resValue "string", "APK_VALIDATE_DEBUG", "$System.env.APK_VALIDATE_DEBUG"
            resValue "string", "APK_VALIDATE_PROD", "$System.env.APK_VALIDATE_PROD"
            resValue "string", "APK_LICENSE_PREMIUM", "$System.env.APK_LICENSE_PREMIUM"

            resValue "string", "ADMOB_COMP_KEY", "$System.env.ADMOB_COMP_KEY"
            resValue "string", "ADMOB_APP_KEY", "$System.env.ADMOBAPPKEY"
            resValue "string", "ADMOB_ADS_KEY", "$System.env.ADMOBADSKEY"

            resValue "string", "analiticsapikey", "$System.env.GOOGLE_ANALITICS_ID"
            resValue "string", "googleapikey", "$System.env.GOOGLEAPIKEY"
            resValue "string", "app_name_domoticz", 'Domoticz'
            buildConfigField "boolean", "LITE_VERSION", "false"
            buildConfigField "boolean", "PAID_OOTT", "false"
            dimension "tier"
        }
        oott {
            applicationId 'nl.hnogames.oott'

            resValue "string", "APK_VALIDATE_DEBUG", "$System.env.APK_VALIDATE_OOTT_DEBUG"
            resValue "string", "APK_VALIDATE_PROD", "$System.env.APK_VALIDATE_OOTT_PROD"
            resValue "string", "APK_LICENSE_PREMIUM", "$System.env.APK_LICENSE_OOTT"

            resValue "string", "ADMOB_COMP_KEY", "$System.env.ADMOB_COMP_KEY"
            resValue "string", "ADMOB_APP_KEY", "$System.env.ADMOBAPPKEY"
            resValue "string", "ADMOB_ADS_KEY", "$System.env.ADMOBADSKEY"

            resValue "string", "analiticsapikey", "$System.env.GOOGLE_ANALITICS_ID"
            resValue "string", "googleapikey", "$System.env.GOOGLEAPIKEY"

            resValue "string", "app_name_domoticz", 'OOTT'
            buildConfigField "boolean", "LITE_VERSION", "false"
            buildConfigField "boolean", "PAID_OOTT", "true"
            dimension "tier"
        }
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            if (dx.additionalParameters == null) {
                dx.additionalParameters = []
            }
            dx.additionalParameters += '--multi-dex'
            dx.additionalParameters += "--main-dex-list=$projectDir/<filename>".toString()
        }
    }

    //noinspection GroovyAssignabilityCheck
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "4g"
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}


allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
}

//noinspection GroovyAssignabilityCheck
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    def supportVersion = '28.0.0'
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:support-v13:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"

    implementation "com.google.firebase:firebase-core:16.0.4"
    implementation "com.google.firebase:firebase-crash:16.2.1"
    implementation "com.google.firebase:firebase-perf:16.1.2"
    implementation "com.google.firebase:firebase-messaging:17.3.3"
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.5"

    implementation 'com.google.android.ads.consent:consent-library:1.0.6'

    implementation "com.google.android.gms:play-services-analytics:16.0.4"
    implementation "com.google.android.gms:play-services-wearable:16.0.1"
    implementation "com.google.android.gms:play-services-maps:16.0.0"
    implementation "com.google.android.gms:play-services-gcm:16.0.0"
    implementation "com.google.android.gms:play-services-location:16.0.0"
    implementation "com.google.android.gms:play-services-places:16.0.0"
    implementation "com.google.android.gms:play-services-ads:16.0.0"

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'fr.avianey.com.viewpagerindicator:library:2.4.1@aar'
    implementation 'com.larswerkman:lobsterpicker:1.0.1'

    implementation 'com.github.zurche:plain-pie:v0.2.2'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    implementation 'com.github.dexafree:MaterialList:v3.2.2'
    implementation 'com.github.hotchemi:android-rate:1.0.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.nhaarman.listviewanimations:lib-core:3.1.0@aar'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.github.jd-alexander:LikeButton:0.2.3'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.4'
    implementation 'com.github.javiersantos:PiracyChecker:1.1'
    implementation 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    implementation("com.mikepenz:materialdrawer:6.0.9@aar") {
        transitive = true
    }
    implementation 'it.sephiroth.android.library.bottomnavigation:bottom-navigation:2.0.1-rc1'
    implementation 'com.github.matthiasrobbers:shortbread:1.0.2'
    annotationProcessor 'com.github.matthiasrobbers:shortbread-compiler:1.0.1'
    implementation 'com.schibstedspain.android:leku:3.3.0'
    implementation 'com.github.k0shk0sh:PermissionHelper:1.1.0'
    implementation 'com.github.zagum:SpeechRecognitionView:1.0.2'
    implementation 'com.afollestad:digitus:0.3.0'

    implementation project(':libs:MemorizingTrustManager')

    implementation project(path: ':materialIntro_library')
    implementation project(path: ':SeekArc_library')
    implementation project(path: ':domoticzapi')
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
